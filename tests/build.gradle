apply plugin: 'scala'
apply plugin: 'eclipse'
compileTestScala.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    mavenLocal()
}

tasks.withType(Test) {
    systemProperties = System.getProperties() // Forward defined properties to the test JVM
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false } // force tests to run every time
}


task testWithoutCredentials(type: Test) {
    exclude '**/*Credentials*'
}

// intended for mainBlueWhisk pipeline
task testBlueCI(type: Test) {
    exclude 'runtime/sdk/**'
}

task testBlueDeployment(type: Test) {
    include 'runtime/integration/**'
    include 'runtime/system/**'
}

task testSDK(type: Test) {
    include 'runtime/sdk/**'
}

task testWithoutSDK(type: Test) {
    exclude 'runtime/sdk/**'
}

dependencies {
    compile "org.scala-lang:scala-library:${gradle.scala.version}"
    compile "org.apache.openwhisk:openwhisk-tests:${gradle.openwhisk.version}:tests"
    compile "org.apache.openwhisk:openwhisk-tests:${gradle.openwhisk.version}:test-sources"
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = gradle.scala.compileFlags
}
